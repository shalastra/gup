plugins {
    id 'application'
    id 'java'
    id 'idea'
    id "com.palantir.graal" version "0.7.1-20-g113a84d"
    id 'com.palantir.git-version' version '0.12.3'
    id "nebula.ospackage" version "8.4.1"
}

group 'io.shalastra'
version gitVersion()
sourceCompatibility = '11'

apply plugin : "java"
apply plugin: 'nebula.deb'

ext {
    javaMainClass = "io.shalastra.MainApplication"
}

application {
    mainClass = javaMainClass
}

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    options.compilerArgs += ["-Aproject=${project.name}"]
}

graal {
    mainClass 'io.shalastra.MainApplication'
    outputName 'mru'
    javaVersion '11'
    option '-H:ResourceConfigurationFiles=' + project.file('resource-config.json').absolutePath
}

processResources {
    expand project.properties
}

dependencies {
    implementation 'com.diogonunes:JColor:5.0.1'

    implementation 'info.picocli:picocli:4.5.1'
    annotationProcessor 'info.picocli:picocli-codegen:4.5.1'

    testImplementation(platform('org.junit:junit-bom:5.7.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

